#!/bin/zsh

setopt local_options no_notify no_monitor

local zsh_plugins=()
if [[ ! -t 0 ]]; then
  # handle both <redirected or piped| input
  while read -r data; do
    # skip comments
    [[ $data != \#* ]] && [[ -n $data ]] || continue
    zsh_plugins+=($data)
  done
elif [[ $# -gt 0 ]]; then
  zsh_plugins=($@)
else
  # if stdin containts no data and no params were passed there's nothing to do
  return 1
fi

# clone the requested plugins in the background
() {
  _antidote_cloneall $zsh_plugins
} &
wait

# script loading of plugins
local pluginstr parts plugin optstr opts
typeset -A opts
local prescript=() script=()

for pluginstr in $zsh_plugins; do
  parts=(${(@s/ /)pluginstr})
  plugin=$parts[1]
  optstr=(${parts[@]:1})
  opts=(${(@s/:/)optstr})

  if [[ $opts[kind] = defer ]] && [[ $#prescript -eq 0 ]]; then
    _antidote_clone romkatv/zsh-defer
    prescript=("$(_antidote_script romkatv/zsh-defer)")
  fi
  script+=("$(_antidote_script $plugin $opts[kind])")
done

printf "%s\n" "${prescript[@]}"
printf "%s\n" "${script[@]}"

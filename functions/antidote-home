#!/bin/zsh

# Translate Windows raw paths to more "POSIX" like Paths
function __fix_windows_path {
  local path=$1
  path=${path//\\//}
  path="/${(L)path}"
  path=${path//:/}
  path=${path// /%20}
  path=${path//\%/%%}
  echo $path
}

### Print where antidote is cloning bundles.
#
# usage: antidote home [-h|--help]
#
# Can be overridden by setting `$ANTIDOTE_HOME`.
#
function antidote-home {
  emulate -L zsh; setopt $_adote_funcopts

  typeset -g REPLY=
  local o_help
  zparseopts $_adote_zparopt_flags -- h=o_help -help=h || return 1

  if (( $#o_help )); then
    antidote-help home
    return
  fi

  local rootdir result
  if [[ -n "$ANTIDOTE_HOME" ]]; then
    result=$ANTIDOTE_HOME
  elif [[ "${OSTYPE}" == darwin* ]]; then
    result="$HOME/Library/Caches/antidote"
  elif [[ "${OSTYPE}" == (cygwin|msys)* ]]; then
    posix_string=$(__fix_windows_path $LOCALAPPDATA)
    result="$posix_string/antidote"
  elif [[ -n "$XDG_CACHE_HOME" ]]; then
    result="$XDG_CACHE_HOME/antidote"
  else
    result="$HOME/.cache/antidote"
  fi
  [[ -d $result ]] || mkdir -p $result
  echo $result
  typeset -g REPLY=$result
}
antidote-home "$@"

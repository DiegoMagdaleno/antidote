#!/usr/bin/env zsh

##? antidote-install -  clone a new bundle and add it to your plugins file
##?
##? usage: antidote install <bundle> [<bundlefile>]
##?
##? args:
##?   <bundle>        Bundle to be installed
##?   [<bundlefile>]  Bundle file to write to if not using the default.
##?                   Defaults to `${ZDOTDIR:-~}/.zsh_plugins.txt` or zstyle setting.

emulate -L zsh
setopt local_options extended_glob

local bundle=$1
local bundlefile=$2

if [[ -z "$bundlefile" ]]; then
  zstyle -s ':antidote:bundle' file \
    'bundlefile' || bundlefile=${ZDOTDIR:-~}/.zsh_plugins.txt
fi

_antidote_clone $bundle
if [[ $? -ne 0 ]]; then
  echo >&2 "antidote: unable to clone bundle '$bundle'."
else
  echo $bundle >>| $bundlefile
  echo "Bundle '$bundle' added to '$bundlefile'."
fi

#!/usr/bin/env zsh

##? antidote-purge - remove a cloned bundle
##?
##? usage: antidote purge <bundle>
##?
##? args:
##?   <bundle>  bundle to be purged

emulate -L zsh
setopt local_options extended_glob

if [[ $# -eq 0 ]]; then
  echo >&2 "antidote: error: required argument 'bundle' not provided, try --help"
  return 1
fi

local bundle=$1
local bundledir=$(_antidote_bundledir $bundle)
if [[ ! -d $bundledir ]]; then
  echo >&2 "antidote: error: $bundle does not exist at the expected location: $bundledir"
  return 1
fi

echo "Removing $bundle..."
command rm -rf "$bundledir"

local bundlefile
zstyle -s ':antidote:bundle' file 'bundlefile' || \
  bundlefile=${ZDOTDIR:-~}/.zsh_plugins.txt
echo "removed!"
echo "You may also need to remove $bundle from '$bundlefile'."

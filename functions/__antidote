#!/usr/bin/env zsh

### The main antidote controller function.

# __antidote allows `antidote` and `antidote-init` to co-exist, which is why this
# code isn't in the core `antidote` function. `antidote-init` will change what that
# function does.

emulate -L zsh
setopt local_options extended_glob
0=${(%):-%x}

local o_help o_version
zparseopts -D -F -K -- \
  {h,-help}=o_help \
  {v,-version}=o_version ||
  return 1

if (( $#o_version )); then
  local ver='1.3.0'
  local gitsha=$(git -C "${0:h:h}" rev-parse --short HEAD 2>/dev/null)
  [[ -z "$gitsha" ]] || ver="$ver ($gitsha)"
  echo "antidote version $ver"

elif (( $#o_help )); then
  antidote-help "$@"

elif [[ $# -eq 0 ]]; then
  antidote-help
  return 2

elif (( $+functions[antidote-${1}] )); then
  local cmd=$1; shift
  antidote-${cmd} "$@"
  return $?

else
  echo >&2 "antidote: command not found '${1}'" && return 1
fi
